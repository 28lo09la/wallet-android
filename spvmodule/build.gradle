apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def localMavenRepo = 'file://' + new File(System.getProperty('user.home'), '.m2/repository').absolutePath

repositories {
    mavenLocal()
   //maven { url localMavenRepo }
    jcenter()
}

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidSdkBuildVersion

    defaultConfig {
        applicationId "com.mycelium.spvmodule"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        all {
            minifyEnabled false
            proguardFiles {
                getDefaultProguardFile('proguard-android.txt')
                'proguard.cfg'
            }
        }
        debug {
            applicationIdSuffix '.test'
            debuggable true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    signingConfigs {
        release {
            keyAlias = ''
        }
        btctestnet {
            keyAlias = ''
        }
        commonDebug {
            storeFile commonDebugKeystore
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    productFlavors {
        btctestnet {
            targetSdkVersion 23
            minSdkVersion 19
            versionName = defaultConfig.versionName + '-TESTNET'
            signingConfig signingConfigs.btctestnet
        }
    }
    buildToolsVersion androidSdkBuildVersion
}

// use the keystore file from the local repository, so that we share the same
// signatures while developing
if (commonDebugKeystore.exists()) {
    android.buildTypes.debug.signingConfig = android.signingConfigs.commonDebug
    // might be needed later on, if we have a devel flavor (if needed at all)
    //android.productFlavors.dev.signingConfig = android.signingConfigs.commonDebug
    logger.info("Using common debug.keystore " + commonDebugKeystore)
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:$junitVersion"
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v4:$androidSupportVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile 'org.bitcoinj:bitcoinj-core:0.15-SNAPSHOT'

    compile project(includePrefix +':spvmodulecontract')
    compile project(includePrefix +':myceliummodularizationtools')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'org.slf4j:slf4j-api:1.7.22'
    compile('com.github.tony19:logback-android-classic:1.1.1-6') {
        exclude(group: 'com.google.android', module: 'android')
    }
    compile 'com.github.tony19:logback-android-core:1.1.1-6'
}

if (project.hasProperty('testKeyStore') &&
        project.hasProperty('testKeyAlias') &&
        project.hasProperty('testKeyStorePassword') &&
        project.hasProperty('testKeyAliasPassword')) {
    android.signingConfigs.btctestnet.storeFile = file('../../' + testKeyStore)
    android.signingConfigs.btctestnet.keyAlias = testKeyAlias
    android.signingConfigs.btctestnet.storePassword = testKeyStorePassword
    android.signingConfigs.btctestnet.keyPassword = testKeyAliasPassword
    android.defaultConfig.applicationId "com.mycelium.spvmodule_testrelease"
} else {
    android.productFlavors.btctestnet.signingConfig = null
}
