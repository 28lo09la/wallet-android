apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

repositories {
    jcenter()
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath androidBuildArtifact
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

configurations {
    compile.exclude group: 'org.apache.httpcomponents', module: 'httpclient'
}

dependencies {
    androidTestCompile "junit:junit:$junitVersion"
    testCompile "junit:junit:$junitVersion"

    compile project(includePrefix +':libs:zxing-android')
    compile project(includePrefix +':coinapult')
    compile project(includePrefix +':wapi')
    compile project(includePrefix +':trezor')
    compile project(includePrefix +':lt-api')
    compile project(includePrefix +':mbwlib')
    compile project(includePrefix +':btchip')
    compile project(includePrefix +':LVL')

    compile guavaArtifact
    compile 'com.squareup:otto:1.3.4'
    // TODO: upgrade this! and remove the exclude support-v4 below
    compile "com.android.support:appcompat-v7:$androidSupportVersion"
    compile "com.android.support:support-v4:$androidSupportVersion"
    compile "com.android.support:animated-vector-drawable:$androidSupportVersion"
    //not upgrading. This will be removed from the core app
    compile('com.google.android.gms:play-services-base:9.0.2') {
        exclude module: 'support-v4'
    }
    compile('com.google.android.gms:play-services-gcm:9.0.2') {
        exclude module: 'support-v4'
    }
    compile 'org.bitcoinj:bitcoinj-core:0.14.5'
    compile 'org.ocpsoft.prettytime:prettytime:3.2.5.Final'
    compile 'de.cketti.library.changelog:ckchangelog:1.2.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'

    compile 'com.jakewharton:butterknife:8.5.1'
    kapt 'com.jakewharton:butterknife-compiler:8.5.1'

    compile 'io.reactivex:rxandroid:1.1.0'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.android.support:multidex:1.0.1'
    compile('com.google.code.findbugs:annotations:3.0.1') {
        exclude module: 'jsr305'
    }
    compile project(includePrefix +':myceliummodularizationtools')
    compile project(includePrefix +':spvmodulecontract')
    compile 'commons-codec:commons-codec:1.5'
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    compile 'com.google.protobuf:protobuf-java:3.1.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

android {
    configurations {
        all*.exclude module: 'jcip-annotations'
        all*.exclude module: 'jsr305'
    }
    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES'
        // bitcoinj brings in a native library for x86_64, which seems to confuse the playstore upload - just strip the file
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
    }

    compileOptions {
        sourceCompatibility = 'VERSION_1_7'
        targetCompatibility = 'VERSION_1_7'
    }

    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidSdkBuildVersion

    defaultConfig {
        versionCode 281100
        versionName '2.8.11'
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias = ''
        }
        btctestnet {
            keyAlias = ''
        }
        commonDebug {
            storeFile commonDebugKeystore
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled !project.hasProperty('skipProguard')
            proguardFile 'mbw.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    if (file != null && file.name.endsWith('.apk')) {
                        def versionExtension = "-" + defaultConfig.versionName + "build" + defaultConfig.versionCode + ".apk"
                        output.outputFile = new File(file.parent, file.name.replace(".apk", versionExtension))
                    }
                }
            }
        }
        debug {
            debuggable true
        }
    }

    productFlavors {
        prodnet {
            targetSdkVersion 23
            minSdkVersion 19

            applicationId 'com.mycelium.wallet'
            versionName = defaultConfig.versionName
            signingConfig signingConfigs.release
        }
        dev {
            buildToolsVersion androidSdkBuildVersion
            minSdkVersion 21

            // if we are not on master, include the branchname in appId
            def appId = 'com.mycelium.devwallet';
            def branch = getGitBranch()

            if (branch != "" && branch != "master") {
                appId = appId + "_spore" // pinning the name to "spore" as multi-module communication depends on package names
                versionName = defaultConfig.versionName + "-DEV_" + branch
            }

            applicationId appId
            signingConfig signingConfigs.debug
        }
        devprod {
            buildToolsVersion androidSdkBuildVersion
            minSdkVersion 21

            def appId = 'com.mycelium.devprodwallet';

            applicationId appId
            signingConfig signingConfigs.debug
        }
        btctestnet {
            targetSdkVersion 23
            minSdkVersion 19

            applicationId 'com.mycelium.testnetwallet'
            versionName = defaultConfig.versionName + '-TESTNET'
            signingConfig signingConfigs.btctestnet
        }
    }
}

// use the keystore file from the local repository, so that we share the same
// signatures while developing
if (commonDebugKeystore.exists()) {
    android.buildTypes.debug.signingConfig = android.signingConfigs.commonDebug
    android.productFlavors.dev.signingConfig = android.signingConfigs.commonDebug
    logger.info("Using common debug.keystore " + commonDebugKeystore)
}

if (project.hasProperty('prodKeyStore') &&
        project.hasProperty('prodKeyAlias') &&
        project.hasProperty('prodKeyStorePassword') &&
        project.hasProperty('prodKeyAliasPassword')) {
    android.signingConfigs.release.storeFile = file('../../' + prodKeyStore)
    android.signingConfigs.release.keyAlias = prodKeyAlias
    android.signingConfigs.release.storePassword = prodKeyStorePassword
    android.signingConfigs.release.keyPassword = prodKeyAliasPassword
} else {
    android.productFlavors.prodnet.signingConfig = null
}

if (project.hasProperty('testKeyStore') &&
        project.hasProperty('testKeyAlias') &&
        project.hasProperty('testKeyStorePassword') &&
        project.hasProperty('testKeyAliasPassword')) {
    android.signingConfigs.btctestnet.storeFile = file('../../' + testKeyStore)
    android.signingConfigs.btctestnet.keyAlias = testKeyAlias
    android.signingConfigs.btctestnet.storePassword = testKeyStorePassword
    android.signingConfigs.btctestnet.keyPassword = testKeyAliasPassword
} else {
    android.productFlavors.btctestnet.signingConfig = null
}
